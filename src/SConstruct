import os

env = Environment()

def CheckPkg(context,pkg):
  context.Message('Checking for %s... ' % pkg)
  rv = context.TryAction('pkg-config --exists \'%s\'' % pkg)[0]
  context.Result(rv)
  return rv

conf = Configure(env,{ "CheckPkg" : CheckPkg })

if not conf.CheckPkg('libnewt'):
  print 'libnewt was not found.'
  Exit(1)

if not conf.CheckPkg('pacman'):
  print 'pacman was not found.'
  Exit(1)

if not conf.CheckPkg('blkid'):
  print 'blkid was not found.'
  Exit(1)

conf.Finish()

try:
  cflags = os.environ['CFLAGS']
except KeyError:
  cflags = '-O2'
  pass

env.Append(CFLAGS = cflags)

env.Append(CFLAGS = '-std=gnu99 -Wall -Wextra -ggdb3 -D_GNU_SOURCE -DUI_NEWT')

env.ParseConfig('pkg-config --cflags --libs pacman libnewt blkid')

env.Program('fwsetup',[
  'main.c',
  'utility.c',
  'block.c',
  'format.c',
  'install.c',
  'postconfig.c',
  'ui_newt.c'
])
